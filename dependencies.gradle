//file:noinspection DependencyNotationArgument

repositories {
    exclusiveContent {
        forRepository {
            maven {
                name 'CurseMaven'
                url 'https://cursemaven.com'
            }
        }
        filter {
            includeGroup 'curse.maven'
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name 'Modrinth'
                url 'https://api.modrinth.com/maven'
            }
        }
        filter {
            includeGroup 'maven.modrinth'
        }
    }
    mavenLocal() // Must be last for caching to work
}

dependencies {

    // Util dependencies
    if (project.use_util.toBoolean()) {
        implementation rfg.deobf("maven.modrinth:fluidlogged-api:2.2.7")
        implementation rfg.deobf("curse.maven:the-one-probe-community-edition-885670:4746213")
        implementation rfg.deobf("curse.maven:had-enough-items-557549:4571247")

        //runtimeOnly("curse.maven:fancy-spawn-eggs-302277:2612571")
        //runtimeOnly("maven.modrinth:better-biome-blend:1.12.2-1.1.7-forge")
        //runtimeOnly("curse.maven:spark-361579:3542217")
    }

    // GregTech dependencies
    if (project.use_gregtech.toBoolean()) {
        implementation rfg.deobf("maven.modrinth:gregtech-ce-unofficial:2.7.3-beta")
        implementation rfg.deobf("maven.modrinth:gregicality-multiblocks:1.2.5")

        runtimeOnly("curse.maven:3.2.3.358-codechicken-lib-1-8-242818:2779848")
        runtimeOnly("curse.maven:ctm-267602:2915363")
    }

    // Dramatic Trees dependencies
    if (project.use_dramatic_trees.toBoolean())
        implementation rfg.deobf("maven.modrinth:dtree:0.13.2")


    // Tinkers Construct dependencies
    if (project.use_tconstruct.toBoolean()) {
        implementation rfg.deobf("curse.maven:2.13.0.183-tinkers-construct-74072:2902483")
        implementation rfg.deobf("curse.maven:1.2.5.10-constructs-armory-287683:3174535")
        implementation rfg.deobf("curse.maven:0.4.3-tinkers-complement-272671:2843439")
        implementation rfg.deobf("curse.maven:1.3.3.55-mantle-74924:2713386")
        if (project.use_util.toBoolean())
            runtimeOnly("curse.maven:1.2-tinkers-jei-291786:2701203")
    }

    // Forestry dependencies
    if (project.use_forestry.toBoolean()) {
        implementation rfg.deobf("curse.maven:5.8.2.422-forestry-59751:2918418")
        implementation rfg.deobf("maven.modrinth:binnies-mods-patched:2.5.1.210")
        if (project.use_dramatic_trees.toBoolean())
            runtimeOnly("curse.maven:dynamic-trees-forestry-401558:3261440")
        if (project.use_util.toBoolean())
            runtimeOnly("curse.maven:jei-bees-248370:2490058")
    }

    // Pams Harvestcraf dependencies
    if (project.use_pams.toBoolean()) {
        implementation rfg.deobf("maven.modrinth:pams-harvestcraft:1.12.2zg")
        if (project.use_util.toBoolean())
            runtimeOnly("curse.maven:just-enough-harvestcraft-267939:2997129")
    }

}
